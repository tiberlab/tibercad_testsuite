#!/usr/bin/env perl

use strict;

use TestSuite qw(run_test
                 read_configuration
                 get_option
                 set_option
                 check_compilation
                 send_email
                 cleanup_test_dir);

use Term::ANSIColor;
use Getopt::Long;
use Cwd;
use Net::Domain qw(hostname);


sub usage {
print "
USAGE: run_tests [OPTIONS]

  OPTIONS are:
    --verbose | -v              : do more output
    --quiet | --noverbose       : do less output
    --config=<file> | -c <file> : use <file> as global configuration file
    --mail | -m                 : send error report to recipients specified
                                  in configuration
    --test=<a,b,c,..> | -t <..> : perform tests a, b, c. The names can contain
                                  glob patterns, but then they must be quoted
    --skip                      : only check data, don't run the tests
    --purge | -p                : delete output of each test after run if
                                  test was succesful. If specified more than
                                  once, deletes output in any case
    --help | -h                 : this help message
    --info | -i                 : print info about the repository

  Tests can be:
    directory name : run make in the named directory and perform checks
                      according to the configuration in this directory.
    \"compile\"      : compile in the toplevel directory
    \"all\"          : do all tests

";

exit;
}



my $configfile = "test.conf";


# for the final summary
my %checklist;

# if we need to inform the developers
our $failure_detected = 0;

# level of verbosity (default: false)
our $verbose = '';

# delete output after run (default: false)
our $purge = 0;

# the tests to perform
our @tests = ();

# skip running the tests
our $skip = '';

# whether to send mail
our $sendmail = '';

# print repository info
our $info = '';


## check a test result and set the global result accordingly
sub check($) {
  $failure_detected = 1 if ($_[0] != 0);
}



##
## main
##

## parse the options
GetOptions('verbose!' => \$verbose,
           'quiet' => sub {$verbose = 0},
           'purge+' => \$purge,
           'config=s' => \$configfile,
           'mail!' => \$sendmail,
           'skip!' => \$skip,
           'test=s' => \@tests,
           'help' => \&usage,
           'info' => \$info);


my @temp = split(/,/,join(',',@tests));
@tests = ();
foreach (@temp) {
  if ($_ eq "compile") {
    push (@tests, $_);
  }
  else {
    push (@tests, glob($_));
  }
}


set_option('verbose', $verbose);
set_option('testonly', $skip);


read_configuration($configfile);



my $topdir = get_option("topdir");

# get some repository info
my @svninfo = (`svn info $topdir`);
($#svninfo <= 0) && die "Is $topdir under subversion control?";
my $repo = (grep(/URL/, @svninfo))[0];
my $revision = (grep(/Revision/, @svninfo))[0];
my $last_changed_author = (grep(/Author/, @svninfo))[0];
chomp($repo);
chomp($revision);
chomp($last_changed_author);

((@tests eq 0) and not $info) && usage();

my $datetime = localtime;
print("\nStarting TiberCAD test suite at $datetime\n");
print("\nRepository info:\n") if $info;
print("     $repo\n") if $info;
print("     $revision\n") if $info;
print("     $last_changed_author\n") if $info;


if (@tests eq 0) { print("\nNo test specified\n"); }


if (scalar grep(/^compile$|^all$/, @tests)) {
  my $compile = check_compilation($topdir);
  check($compile);
  $checklist{'compilation'} = $compile;
}


if (scalar grep(/^all$/, @tests)) {
  opendir CWD, ".";
  foreach (sort(readdir CWD)) {
    my $dir = $_;

    (($dir =~ /^test[0-9]+/) && (-d $dir)) || next;
    push(@tests, $dir);
  }
}


foreach (@tests) {
  my $dir = $_;

  (-d $dir) || next;

  my $result = run_test($dir);
  check($result);
  $checklist{$dir} = $result;

  if (($purge > 1) || ($purge && not $result)) {
    cleanup_test_dir($dir);
  }
}


print(color("red"), "\n*** WARNING ***  Errors detected in test suite.\n",
      color("reset")) if ($failure_detected == 1);


if ($failure_detected and $sendmail) {

  my $msg = "Errors detected in TiberCAD test suite.\n\n";
  $msg = "${msg}Repository info:\n";
  $msg = "${msg}     $repo\n";
  $msg = "${msg}     $revision\n";
  $msg = "${msg}     $last_changed_author\n\n";
  $msg = "${msg}Run: $datetime\n\n";
  $msg = "${msg}The following tests failed:\n";

  while (my ($test, $result) = each(%checklist)) {
    $msg = "${msg}   $test\n" if $result;
  }

  my $cwd = cwd();
  my $host = hostname();
  $msg = "${msg}\nYou can find the output data and logs of the ";
  $msg = "${msg}failed tests\non ${host} in ${cwd}/testXX.\n";
  $msg = "${msg}Use 'run_tests -v --skip --test testXX' in the ";
  $msg = "${msg}testsuite directory to find out more.\n";

  my %message = (
    'subject' => "TiberCAD test suite error report\n",
    'message' => $msg);
  send_email(\%message);
}


$datetime = localtime;
print "\nTiberCAD test suite finished at ", $datetime, "\n\n";
