#!/usr/bin/env perl
#===============================================================================
#  File     run_tests
#  Author:     Matthias Auf der Maur <auf.der.maur@ing.uniroma2.it>
#  Description: executable for the simple testsuite for tibercad software
#
#  Copyright (C) 2025  Matthias Auf der Maur
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <https://www.gnu.org/licenses/>.
#===============================================================================

use strict;

use lib '.';

use TestSuite qw(run_test
                 read_configuration
                 get_option
                 set_option
                 check_compilation
                 send_email
                 cleanup_test_dir);

use Term::ANSIColor;
use Getopt::Long;
use Cwd;
use Net::Domain qw(hostname);


sub usage {
print "
USAGE: run_tests [OPTIONS]

  OPTIONS are:
    --verbose | -v              : do more output
    --quiet | --noverbose       : do less output
    --config=<file> | -c <file> : use <file> as global configuration file
    --mail | -m                 : send error report to recipients specified
                                  in configuration
    --test=<a,b,c,..> | -t <..> : perform tests a, b, c. The names can contain
                                  glob patterns, but then they must be quoted
    --skip                      : only check data, don't run the tests
    --purge | -p                : delete output of each test after run if
                                  test was succesful. If specified more than
                                  once, deletes output in any case
    --help | -h                 : this help message
    --info | -i                 : print info about the repository

  Tests can be:
    directory name : run make in the named directory and perform checks
                      according to the configuration in this directory.
    \"compile\"      : compile in the toplevel directory
    \"all\"          : do all tests

";

exit;
}



my $configfile = "test.conf";


# for the final summary
my %checklist;

# if we need to inform the developers
our $failure_detected = 0;

# level of verbosity (default: false)
our $verbose = '';

# delete output after run (default: false)
our $purge = 0;

# the tests to perform
our @tests = ();

# skip running the tests
our $skip = '';

# whether to send mail
our $sendmail = '';

# print repository info
our $info = '';


## check a test result and set the global result accordingly
sub check($) {
  $failure_detected = 1 if ($_[0] != 0);
}

## print repository info
sub repository_info($) {

  $info = $_[0];

  print("\nRepository info:\n") if $info;

  my $topdir = get_option("topdir");

  my $svncmd = get_option("svn");
  my $gitcmd = get_option("git");

  my $revision = "";

  my @svninfo = (`$svncmd info $topdir`);
  if ($#svninfo <= 0) {
    # retry with git
    $revision = (`cd $topdir && $gitcmd describe --always --tags --dirty`);
    chomp($revision);
    print("     git revision: $revision\n") if $info;
  }
  else {

    my $repo = (grep(/URL/, @svninfo))[0];
    $revision = (grep(/Revision/, @svninfo))[0];
    my $last_changed_author = (grep(/Author/, @svninfo))[0];
    chomp($repo);
    chomp($revision);
    chomp($last_changed_author);

    print("     $repo\n") if $info;
    print("     $revision\n") if $info;
    print("     $last_changed_author\n") if $info;
  }

  return $revision;
}



##
## main
##

## parse the options
GetOptions('verbose!' => \$verbose,
           'quiet' => sub {$verbose = 0},
           'purge+' => \$purge,
           'config=s' => \$configfile,
           'mail!' => \$sendmail,
           'skip!' => \$skip,
           'test=s' => \@tests,
           'help' => \&usage,
           'info' => \$info);


my @temp = split(/,/,join(',',@tests));
@tests = ();
foreach (@temp) {
  if ($_ eq "compile") {
    push (@tests, $_);
  }
  else {
    push (@tests, glob($_));
  }
}


set_option('verbose', $verbose);
set_option('testonly', $skip);


read_configuration($configfile);



my $topdir = get_option("topdir");

((@tests eq 0) and not $info) && usage();

my $datetime = localtime;
print("\nStarting TiberCAD test suite at $datetime\n");

my $revision = repository_info($info);


if (@tests eq 0) { print("\nNo test specified\n"); }


if (scalar grep(/^compile$|^all$/, @tests)) {
  my $compile = check_compilation($topdir);
  check($compile);
  $checklist{'compilation'} = $compile;
}


if (scalar grep(/^all$/, @tests)) {
  opendir CWD, ".";
  foreach (sort(readdir CWD)) {
    my $dir = $_;

    (($dir =~ /^test[0-9]+/) && (-d $dir)) || next;
    push(@tests, $dir);
  }
}


foreach (@tests) {
  my $dir = $_;

  (-d $dir) || next;

  my $result = run_test($dir);
  check($result);
  $checklist{$dir} = $result;
  if ($result) {
    print "Test ",$dir," failed.\n";
  }

  if (($purge > 1) || ($purge && not $result)) {
    cleanup_test_dir($dir);
  }
}


print(color("red"), "\n*** WARNING ***  Errors detected in test suite.\n",
      color("reset")) if ($failure_detected == 1);


if ($failure_detected and $sendmail) {

  my $msg = "Errors detected in TiberCAD test suite.\n\n";
  $msg = "${msg}Repository info: $revision\n\n";
  $msg = "${msg}Run: $datetime\n\n";
  $msg = "${msg}The following tests failed:\n";

  while (my ($test, $result) = each(%checklist)) {
    $msg = "${msg}   $test\n" if $result;
  }

  my $cwd = cwd();
  my $host = hostname();
  $msg = "${msg}\nYou can find the output data and logs of the ";
  $msg = "${msg}failed tests\non ${host} in ${cwd}/testXX.\n";
  $msg = "${msg}Use 'run_tests -v --skip --test testXX' in the ";
  $msg = "${msg}testsuite directory to find out more.\n";

  my %message = (
    'subject' => "TiberCAD test suite error report\n",
    'message' => $msg);
  send_email(\%message);
}



$datetime = localtime;
print "\nTiberCAD test suite finished at ", $datetime, "\n\n";
